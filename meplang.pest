WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" | "//" ~ (!NEWLINE ~ ANY)*}

hex_litteral = @{"0x" ~ (HEX_DIGIT ~ HEX_DIGIT) * }
word = @{ ASCII_ALPHA+ }
keyword = @{ word ~ &WHITESPACE }
var_name = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
var_with_dots = @{ var_name ~ ("." ~ var_with_dots)? }

expression = { var_name ~ "=" ~ hex_litteral }

attribute = {
	"#[" ~ var_name ~ 
    ("(" ~ expression  ~ ")")?
    ~ "]"
}

const_decl = { keyword ~ expression ~ ";"}

function = ${
	var_name ~ WHITESPACE* ~ "("
    	~ WHITESPACE* ~ (var_with_dots | hex_litteral) ~ WHITESPACE* ~
    ")"
}

block_content_inner = ${
	hex_litteral |
	function |
	var_name ~ !(WHITESPACE* ~ "(")
}

block_content = @{block_content_inner ~ &(WHITESPACE | "}" | EOI)}

block_decl = {
	keyword ~ var_name ~ "{" ~
    	block_content*
     ~ "}"
}

block_decl_with_attr = {
	attribute* ~ block_decl
}

contract_decl = {
	keyword ~ var_name ~ "{" ~
    	(block_decl_with_attr | const_decl)*
    ~ "}"
}

contract_decl_with_attr = {
	attribute* ~ contract_decl
}

file = {
	SOI ~ 
	contract_decl_with_attr* ~
	EOI
}