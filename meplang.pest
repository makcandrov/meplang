WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" | "//" ~ (!NEWLINE ~ ANY)*}

semicolon = { ";" }
equal = { "=" }

string_inner = ${(!"\"" ~ ANY)*}

string_litteral = @{ "\"" ~ string_inner ~ "\"" }
hex_litteral = @{"0x" ~ HEX_DIGIT * ~ !ASCII_ALPHANUMERIC }

litteral = { string_litteral | hex_litteral }

var_name = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
var_with_dots = @{ var_name ~ ("." ~ var_with_dots)? }

const_keyword = @{ "const" ~ &WHITESPACE }
block_keyword = @{ "block" ~ &WHITESPACE }
contract_keyword = @{ "contract" ~ &WHITESPACE }

affectation = { var_name ~ equal ~ litteral }

attribute = {
	"#[" ~ var_name ~ 
    ("(" ~ affectation  ~ ")")?
    ~ "]"
}

const_decl = { const_keyword ~ affectation ~ semicolon }

function = ${
	var_name ~ WHITESPACE* ~ "("
    	~ WHITESPACE* ~ (var_with_dots | hex_litteral) ~ WHITESPACE* ~
    ")"
}

block_line = ${
	hex_litteral |
	function |
	var_name ~ !(WHITESPACE* ~ "(")
}

block_content = @{block_line ~ &(WHITESPACE | "}" | EOI)}

block_decl = {
	block_keyword ~ var_name ~ "{" ~
    	block_content*
     ~ "}"
}

block_decl_with_attr = {
	attribute* ~ block_decl
}

contract_decl = {
	contract_keyword ~ var_name ~ "{" ~
    	(block_decl_with_attr | const_decl)*
    ~ "}"
}

contract_decl_with_attr = {
	attribute* ~ contract_decl
}

file = {
	SOI ~ 
	contract_decl_with_attr+ ~
	EOI
}
