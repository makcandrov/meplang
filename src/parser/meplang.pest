WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" | "//" ~ (!NEWLINE ~ ANY)* }

dot = { "." }
semicolon = { ";" }
eq = { "=" }
open_paren = { "(" }
close_paren = { ")" }
open_brace = { "{" }
close_brace = { "}" }
star = { "*" }
esp = { "&" }

string_inner = ${ (!"\"" ~ ANY)* }

string_litteral = @{ "\"" ~ string_inner ~ "\"" }
hex_litteral = @{"0x" ~ HEX_DIGIT * ~ !ASCII_ALPHANUMERIC }

hex_or_string_litteral = { string_litteral | hex_litteral }

variable = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
variable_with_field = ${ variable ~ dot ~ variable }
variable_or_variable_with_field = { variable_with_field | variable }

const_keyword = @{ "const" ~ &WHITESPACE }
block_keyword = @{ "block" ~ &WHITESPACE }
contract_keyword = @{ "contract" ~ &WHITESPACE }

equality = { variable ~ eq ~ hex_or_string_litteral }

attribute_arg = { equality | variable | hex_or_string_litteral }
function_arg = { variable_with_field | hex_litteral | variable }

attribute = {
	"#[" ~ variable ~ 
    (open_paren ~ attribute_arg ~ close_paren)?
    ~ "]"
}

const_decl = { const_keyword ~ variable ~ eq ~ hex_litteral ~ semicolon }

function = ${
	variable ~ WHITESPACE* ~ open_paren
    	~ WHITESPACE* ~ function_arg ~ WHITESPACE* ~
    close_paren
}

block_ref = { (star ~ variable) | (esp ~ variable_or_variable_with_field) }

block_item = ${
	hex_litteral |
	block_ref |
	function |
	variable ~ !(WHITESPACE* ~ open_paren)
}

block_item_with_attr = { attribute* ~ block_item }

block_decl = {
	block_keyword ~ variable ~ open_brace ~
    	block_item_with_attr*
     ~ close_brace
}

block_decl_with_attr = {
	attribute* ~ block_decl
}

contract_decl = {
	contract_keyword ~ variable ~ open_brace ~
    	(block_decl_with_attr | const_decl)*
    ~ close_brace
}

contract_decl_with_attr = {
	attribute* ~ contract_decl
}

file = {
	SOI ~ 
	contract_decl_with_attr+ ~
	EOI
}
